# LibPNG dependency
# https://sourceforge.net/projects/libpng
#
# Output target: LibPNG


include(${EXTERNALPROJECT_INCLUDE_DIR}/external_project_common.cmake)

ExternalProject_Get_Property(ZLIB INSTALL_DIR)
set(ZLIB_INSTALL_DIR ${INSTALL_DIR})
unset(INSTALL_DIR)

ExternalProject_Add(PNG
    PREFIX ${EXTERNALPROJECT_BINARY_ROOT}/png
    URL "https://github.com/pnggroup/libpng/archive/refs/tags/v1.6.55.zip"
    URL_MD5 "f64ff4e41a5bbbea9e1432b3b13947f1"
    DOWNLOAD_DIR "${EXTERNALPROJECT_SOURCE_ROOT}/png"
    SOURCE_DIR "${EXTERNALPROJECT_SOURCE_PREFIX}/png/source"
    BINARY_DIR "${EXTERNALPROJECT_BINARY_ROOT}/png/build"
    INSTALL_DIR "${EXTERNALPROJECT_BINARY_ROOT}/png/install"
    DOWNLOAD_EXTRACT_TIMESTAMP TRUE
    UPDATE_COMMAND ""
    PATCH_COMMAND ""
    BUILD_COMMAND ${BUILD_COMMAND_FOR_TARGET}
    INSTALL_COMMAND ${BUILD_COMMAND_FOR_TARGET} -t install
    CMAKE_ARGS ${CMAKE_TOOLCHAIN_FILE_ARG} ${CMAKE_BUILD_TYPE_ARG} "-DZLIB_USE_STATIC_LIBS:BOOL=ON" "-DZLIB_ROOT:PATH=${ZLIB_ROOT}" 
        "-DPNG_SHARED=OFF" "-DPNG_STATIC=ON" "-DPNG_TESTS=OFF" "-DPNG_TOOLS=OFF" "-DPNG_FRAMEWORK=OFF"
        "-DPNG_DEBUG_POSTFIX=" "-DPNG_BUILD_ZLIB=OFF" "-DPNG_HARDWARE_OPTIMIZATIONS=ON" "-DCMAKE_C_FLAGS:STRING=${ZERO_WARNINGS_FLAG} ${FPIC_FLAG}"
        "-DCMAKE_INSTALL_PREFIX:PATH=${EXTERNALPROJECT_BINARY_ROOT}/png/install" "-DSKIP_INSTALL_EXECUTABLES=ON" "-DSKIP_INSTALL_PROGRAMS=ON"
    EXCLUDE_FROM_ALL
    DEPENDS ZLIB
)

ExternalProject_Get_Property(PNG INSTALL_DIR)

add_library(LibPNG INTERFACE)
add_dependencies(LibPNG PNG)
if(UNIX)
    target_link_libraries(LibPNG INTERFACE ${INSTALL_DIR}/lib/libpng16${CMAKE_STATIC_LIBRARY_SUFFIX})
else()
    target_link_libraries(LibPNG INTERFACE ${INSTALL_DIR}/lib/libpng16_static${CMAKE_STATIC_LIBRARY_SUFFIX})
endif()
target_include_directories(LibPNG INTERFACE ${INSTALL_DIR}/include)
set_property(TARGET PNG PROPERTY FOLDER "Dependencies")

unset(INSTALL_DIR)

