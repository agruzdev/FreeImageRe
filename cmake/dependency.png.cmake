# LibPNG dependency
# https://sourceforge.net/projects/libpng
#
# Output target: LibPNG


include(${EXTERNALPROJECT_INCLUDE_DIR}/external_project_common.cmake)

ExternalProject_Get_Property(ZLIB INSTALL_DIR)
set(ZLIB_INSTALL_DIR ${INSTALL_DIR})
unset(INSTALL_DIR)

ExternalProject_Add(PNG
    PREFIX ${EXTERNALPROJECT_BINARY_ROOT}/png
    URL "https://github.com/pnggroup/libpng/archive/refs/tags/v1.6.54.zip"
    URL_MD5 "1eca555ae2674566a0b988c3ecf2ffaf"
    DOWNLOAD_DIR "${EXTERNALPROJECT_SOURCE_ROOT}/png"
    SOURCE_DIR "${EXTERNALPROJECT_SOURCE_PREFIX}/png/source"
    BINARY_DIR "${EXTERNALPROJECT_BINARY_ROOT}/png/build"
    INSTALL_DIR "${EXTERNALPROJECT_BINARY_ROOT}/png/install"
    DOWNLOAD_EXTRACT_TIMESTAMP TRUE
    UPDATE_COMMAND ""
    PATCH_COMMAND ""
    BUILD_COMMAND ${BUILD_COMMAND_FOR_TARGET} -t png_static
    CMAKE_ARGS ${CMAKE_BUILD_TYPE_ARG} "-DZLIB_ROOT:PATH=${ZLIB_ROOT}" "-DPNG_SHARED=OFF" "-DPNG_STATIC=ON" "-DPNG_TESTS=OFF" "-DPNG_TOOLS=OFF" "-DPNG_FRAMEWORK=OFF" "-DPNG_DEBUG=OFF" 
        "-DPNG_BUILD_ZLIB=OFF" "-DPNG_HARDWARE_OPTIMIZATIONS=ON" "-DCMAKE_C_FLAGS:STRING=${ZERO_WARNINGS_FLAG} -fPIC"
        "-DCMAKE_INSTALL_PREFIX:PATH=${EXTERNALPROJECT_BINARY_ROOT}/png/install" "-DSKIP_INSTALL_EXECUTABLES=ON" "-DSKIP_INSTALL_PROGRAMS=ON"
    EXCLUDE_FROM_ALL
    DEPENDS ZLIB
)

ExternalProject_Get_Property(PNG INSTALL_DIR)

add_library(LibPNG INTERFACE)
add_dependencies(LibPNG PNG)
if(UNIX)
    link_library_path2(LibPNG ${INSTALL_DIR}/lib libpng16${CMAKE_STATIC_LIBRARY_SUFFIX} libpng16d${CMAKE_STATIC_LIBRARY_SUFFIX})
else()
    link_library_path2(LibPNG ${INSTALL_DIR}/lib libpng16_static${CMAKE_STATIC_LIBRARY_SUFFIX} libpng16_staticd${CMAKE_STATIC_LIBRARY_SUFFIX})
endif()
target_include_directories(LibPNG INTERFACE ${INSTALL_DIR}/include)
set_property(TARGET PNG PROPERTY FOLDER "Dependencies")

unset(INSTALL_DIR)

