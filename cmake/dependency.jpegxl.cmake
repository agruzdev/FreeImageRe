# JpegXL dependency
#
# Output target: LibJpegXL
#

include(${EXTERNALPROJECT_INCLUDE_DIR}/external_project_common.cmake)
include(${EXTERNALPROJECT_INCLUDE_DIR}/dependency.zlib.cmake)
include(${EXTERNALPROJECT_INCLUDE_DIR}/dependency.highway.cmake)
include(${EXTERNALPROJECT_INCLUDE_DIR}/dependency.brotli.cmake)
include(${EXTERNALPROJECT_INCLUDE_DIR}/dependency.lcms2.cmake)

ExternalProject_Get_Property(ZLIB INSTALL_DIR)
set(ZLIB_INSTALL_DIR ${INSTALL_DIR})
unset(INSTALL_DIR)


ExternalProject_Add(JPEGXL
    PREFIX ${EXTERNALPROJECT_BINARY_ROOT}/jpegxl
    URL "https://github.com/libjxl/libjxl/archive/refs/tags/v0.11.1.zip"
    URL_MD5 "87b3968e0878edf57b09bb8bfaf4618e"
    DOWNLOAD_DIR "${EXTERNALPROJECT_SOURCE_ROOT}/jpegxl"
    SOURCE_DIR "${EXTERNALPROJECT_SOURCE_PREFIX}/jpegxl/source"
    BINARY_DIR "${EXTERNALPROJECT_BINARY_ROOT}/jpegxl/build"
    INSTALL_DIR "${EXTERNALPROJECT_BINARY_ROOT}/jpegxl/install"
    DOWNLOAD_EXTRACT_TIMESTAMP TRUE
    UPDATE_COMMAND ""
    PATCH_COMMAND ""
    BUILD_COMMAND ${BUILD_COMMAND_FOR_TARGET} -t jxl
    CMAKE_ARGS ${CMAKE_TOOLCHAIN_FILE_ARG} ${CMAKE_BUILD_TYPE_ARG}
        "-DBUILD_SHARED_LIBS=OFF" "-DBUILD_TESTING=OFF" "-DJPEGXL_ENABLE_BENCHMARK=OFF" "-DJPEGXL_ENABLE_EXAMPLES=OFF" "-DJPEGXL_ENABLE_JNI=OFF"
        "-DJPEGXL_ENABLE_JPEGLI=OFF" "-DJPEGXL_ENABLE_JPEGLI_LIBJPEG=OFF" "-DJPEGXL_ENABLE_MANPAGES=OFF" "-DJPEGXL_ENABLE_OPENEXR=OFF" "-DJPEGXL_ENABLE_SJPEG=OFF"
        "-DJPEGXL_ENABLE_TOOLS=OFF" "-DJPEGXL_ENABLE_TOOLS=ON" "-DJPEGXL_WARNINGS_AS_ERRORS=OFF" "-DJPEGXL_ENABLE_SKCMS=OFF"
        "-DJPEGXL_FORCE_SYSTEM_HWY=ON" "-DHWY_ROOT=${HWY_ROOT}"
        "-DJPEGXL_FORCE_SYSTEM_BROTLI=ON" "-DBrotli_ROOT=${Brotli_ROOT}"
        "-DJPEGXL_FORCE_SYSTEM_LCMS2=ON" "-DLCMS2_ROOT=${LCMS2_ROOT}"
        "-DZLIB_USE_STATIC_LIBS=ON" "-DZLIB_ROOT=${ZLIB_ROOT}"
        "-DCMAKE_C_FLAGS:STRING=${ZERO_WARNINGS_FLAG} ${FPIC_FLAG} -DCMS_NO_REGISTER_KEYWORD=1" 
        "-DCMAKE_CXX_FLAGS:STRING=${ZERO_WARNINGS_FLAG} ${EHSC_FLAG} ${FPIC_FLAG} -DCMS_NO_REGISTER_KEYWORD=1 -DJXL_CMS_STATIC_DEFINE=1"
        "-DCMAKE_DEBUG_POSTFIX=" "-DCMAKE_INSTALL_PREFIX:PATH=${EXTERNALPROJECT_BINARY_ROOT}/jpegxl/install"
    EXCLUDE_FROM_ALL
    DEPENDS ZLIB HIGHWAY BROTLI LCMS2
)

ExternalProject_Get_Property(JPEGXL INSTALL_DIR)

add_library(LibJpegXL INTERFACE)
add_dependencies(LibJpegXL JPEGXL)
target_link_libraries(LibJpegXL INTERFACE
    ${INSTALL_DIR}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}jxl${CMAKE_STATIC_LIBRARY_SUFFIX}
    ${INSTALL_DIR}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}jxl_cms${CMAKE_STATIC_LIBRARY_SUFFIX}
)
target_include_directories(LibJpegXL INTERFACE ${INSTALL_DIR}/include)
target_link_libraries(LibJpegXL INTERFACE LibHighway LibBrotli LibLCMS2)
set_property(TARGET JPEGXL PROPERTY FOLDER "Dependencies")

set(JPEGXL_ROOT ${INSTALL_DIR})

unset(INSTALL_DIR)

