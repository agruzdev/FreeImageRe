# Brotli dependency
#
# Output target: LibBrotli
#

include(${EXTERNALPROJECT_INCLUDE_DIR}/external_project_common.cmake)


ExternalProject_Add(BROTLI
    PREFIX ${EXTERNALPROJECT_BINARY_ROOT}/brotli
    URL "https://github.com/google/brotli/archive/refs/tags/v1.2.0.zip"
    URL_MD5 "5b7c422f6c3d7c2362df4d54b6ab36f9"
    DOWNLOAD_DIR "${EXTERNALPROJECT_SOURCE_ROOT}/brotli"
    SOURCE_DIR "${EXTERNALPROJECT_SOURCE_PREFIX}/brotli/source"
    BINARY_DIR "${EXTERNALPROJECT_BINARY_ROOT}/brotli/build"
    INSTALL_DIR "${EXTERNALPROJECT_BINARY_ROOT}/brotli/install"
    DOWNLOAD_EXTRACT_TIMESTAMP TRUE
    UPDATE_COMMAND ""
    PATCH_COMMAND ""
    BUILD_COMMAND ${BUILD_COMMAND_FOR_TARGET}
    INSTALL_COMMAND ${BUILD_COMMAND_FOR_TARGET} -t install
    CMAKE_ARGS ${CMAKE_BUILD_TYPE_ARG} "-DBUILD_SHARED_LIBS=OFF" "-DBUILD_TESTING=OFF" "-DBROTLI_BUILD_FOR_PACKAGE=OFF" "-DBROTLI_BUILD_TOOLS=OFF"
        "-DCMAKE_C_FLAGS:STRING=${ZERO_WARNINGS_FLAG} ${FPIC_FLAG}" "-DCMAKE_DEBUG_POSTFIX=" "-DCMAKE_INSTALL_PREFIX:PATH=${EXTERNALPROJECT_BINARY_ROOT}/brotli/install"
    EXCLUDE_FROM_ALL
)

ExternalProject_Get_Property(BROTLI INSTALL_DIR)

add_library(LibBrotli INTERFACE)
add_dependencies(LibBrotli BROTLI)
target_link_libraries(LibBrotli INTERFACE
    ${INSTALL_DIR}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}brotlidec${CMAKE_STATIC_LIBRARY_SUFFIX}
    ${INSTALL_DIR}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}brotlienc${CMAKE_STATIC_LIBRARY_SUFFIX}
    ${INSTALL_DIR}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}brotlicommon${CMAKE_STATIC_LIBRARY_SUFFIX}
)
target_include_directories(LibBrotli INTERFACE ${INSTALL_DIR}/include)
set_property(TARGET BROTLI PROPERTY FOLDER "Dependencies")

set(Brotli_ROOT ${INSTALL_DIR})

unset(INSTALL_DIR)

