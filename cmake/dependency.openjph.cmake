# OpenJPH dependency (JPEG 2000)
#
# Output target: LibOpenJPH
#

include(${EXTERNALPROJECT_INCLUDE_DIR}/external_project_common.cmake)

ExternalProject_Add(OPENJPH
    PREFIX ${EXTERNALPROJECT_BINARY_ROOT}/openjph
    URL "https://github.com/aous72/OpenJPH/archive/refs/tags/0.26.0.zip"
    URL_MD5 "8a26d7e57e78b5e06c7a4737ace63b5b"
    DOWNLOAD_DIR "${EXTERNALPROJECT_SOURCE_ROOT}/openjph"
    SOURCE_DIR "${EXTERNALPROJECT_SOURCE_PREFIX}/openjph/source"
    BINARY_DIR "${EXTERNALPROJECT_BINARY_ROOT}/openjph/build"
    INSTALL_DIR "${EXTERNALPROJECT_BINARY_ROOT}/openjph/install"
    DOWNLOAD_EXTRACT_TIMESTAMP TRUE
    UPDATE_COMMAND ""
    PATCH_COMMAND ""
    BUILD_COMMAND ${BUILD_COMMAND_FOR_TARGET} -t openjph
    CMAKE_ARGS ${CMAKE_BUILD_TYPE_ARG} "-DBUILD_SHARED_LIBS=OFF" "-DOJPH_BUILD_TESTS=OFF" "-DOJPH_ENABLE_TIFF_SUPPORT=OFF" "-DOJPH_BUILD_EXECUTABLES=OFF"
        "-DCMAKE_C_FLAGS:STRING=${ZERO_WARNINGS_FLAG} -fPIC" "-DCMAKE_DEBUG_POSTFIX=d" "-DCMAKE_INSTALL_PREFIX:PATH=${EXTERNALPROJECT_BINARY_ROOT}/openjph/install"
    EXCLUDE_FROM_ALL
)

ExternalProject_Get_Property(OPENJPH SOURCE_DIR)
ExternalProject_Get_Property(OPENJPH BINARY_DIR)
ExternalProject_Get_Property(OPENJPH INSTALL_DIR)

add_library(LibOpenJPH INTERFACE)
add_dependencies(LibOpenJPH OPENJPH)
if (MSVC)
    link_library_path2(LibOpenJPH ${INSTALL_DIR}/lib ${CMAKE_STATIC_LIBRARY_PREFIX}openjph.0.26${CMAKE_STATIC_LIBRARY_SUFFIX} ${CMAKE_STATIC_LIBRARY_PREFIX}openjph.0.26d${CMAKE_STATIC_LIBRARY_SUFFIX})
else()
    target_link_libraries(LibOpenJPH INTERFACE ${INSTALL_DIR}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}openjph${CMAKE_STATIC_LIBRARY_SUFFIX})
endif()
target_include_directories(LibOpenJPH INTERFACE ${INSTALL_DIR}/include)
set_property(TARGET OPENJPH PROPERTY FOLDER "Dependencies")


set(OPENJPH_ROOT ${INSTALL_DIR})

unset(SOURCE_DIR)
unset(BINARY_DIR)
unset(INSTALL_DIR)

