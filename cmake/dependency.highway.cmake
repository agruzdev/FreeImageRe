# Highway dependency
#
# Output target: LibHighway
#

include(${EXTERNALPROJECT_INCLUDE_DIR}/external_project_common.cmake)

ExternalProject_Add(HIGHWAY
    PREFIX ${EXTERNALPROJECT_BINARY_ROOT}/highway
    URL "https://github.com/google/highway/archive/refs/tags/1.3.0.zip"
    URL_MD5 "e91017527ce45fad36e2e8803ecda565"
    DOWNLOAD_DIR "${EXTERNALPROJECT_SOURCE_ROOT}/highway"
    SOURCE_DIR "${EXTERNALPROJECT_SOURCE_PREFIX}/highway/source"
    BINARY_DIR "${EXTERNALPROJECT_BINARY_ROOT}/highway/build"
    INSTALL_DIR "${EXTERNALPROJECT_BINARY_ROOT}/highway/install"
    DOWNLOAD_EXTRACT_TIMESTAMP TRUE
    UPDATE_COMMAND ""
    PATCH_COMMAND ""
    BUILD_COMMAND ${BUILD_COMMAND_FOR_TARGET}
    INSTALL_COMMAND ${BUILD_COMMAND_FOR_TARGET} -t install
    CMAKE_ARGS ${CMAKE_BUILD_TYPE_ARG} "-DBUILD_SHARED_LIBS=OFF" "-DBUILD_TESTING=OFF" "-DHWY_ENABLE_TESTS=OFF" "-DHWY_ENABLE_EXAMPLES=OFF"
        "-DHWY_FORCE_STATIC_LIBS=ON" "-DHWY_ENABLE_CONTRIB=OFF" "-DHWY_TEST_STANDALONE=OFF" "-DHWY_WARNINGS_ARE_ERRORS=OFF" "-DHWY_CMAKE_HEADER_ONLY=OFF"
        "-DCMAKE_C_FLAGS:STRING=${ZERO_WARNINGS_FLAG} ${FPIC_FLAG}" "-DCMAKE_DEBUG_POSTFIX=" "-DCMAKE_INSTALL_PREFIX:PATH=${EXTERNALPROJECT_BINARY_ROOT}/highway/install"
    EXCLUDE_FROM_ALL
)

ExternalProject_Get_Property(HIGHWAY INSTALL_DIR)

add_library(LibHighway INTERFACE)
add_dependencies(LibHighway HIGHWAY)
target_link_libraries(LibHighway INTERFACE ${INSTALL_DIR}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}hwy${CMAKE_STATIC_LIBRARY_SUFFIX})
target_include_directories(LibHighway INTERFACE ${INSTALL_DIR}/include)
set_property(TARGET HIGHWAY PROPERTY FOLDER "Dependencies")

set(HWY_ROOT ${INSTALL_DIR})

unset(INSTALL_DIR)

