# HIEF dependency
# https://github.com/strukturag/libheif
#
# Output target: LibHEIF


include(${EXTERNALPROJECT_INCLUDE_DIR}/external_project_common.cmake)
include(${EXTERNALPROJECT_INCLUDE_DIR}/dependency.de265.cmake)
include(${EXTERNALPROJECT_INCLUDE_DIR}/dependency.kvazaar.cmake)
include(${EXTERNALPROJECT_INCLUDE_DIR}/dependency.dav1d.cmake)
include(${EXTERNALPROJECT_INCLUDE_DIR}/dependency.svtav1.cmake)

set(HEIF_VERSION "1.20.2")

ExternalProject_Add(HEIF
    PREFIX ${EXTERNALPROJECT_BINARY_ROOT}/heif
    URL "https://github.com/strukturag/libheif/releases/download/v${HEIF_VERSION}/libheif-${HEIF_VERSION}.tar.gz"
    URL_MD5 "5d0442f7197a34b7aaf95bdffabb51e9"
    DOWNLOAD_DIR "${EXTERNALPROJECT_SOURCE_ROOT}/heif"
    SOURCE_DIR "${EXTERNALPROJECT_SOURCE_PREFIX}/heif/source"
    BINARY_DIR "${EXTERNALPROJECT_BINARY_ROOT}/heif/build"
    DOWNLOAD_EXTRACT_TIMESTAMP TRUE
    UPDATE_COMMAND ""
    PATCH_COMMAND ""
    BUILD_COMMAND ${BUILD_COMMAND_FOR_TARGET} -t heif
    INSTALL_COMMAND ${BUILD_COMMAND_FOR_TARGET} -t install
    CMAKE_ARGS ${CMAKE_BUILD_TYPE_ARG}
        "-DBUILD_SHARED_LIBS=ON" "-DBUILD_TESTING=OFF" "-DENABLE_EXPERIMENTAL_FEATURES=OFF" "-DCMAKE_COMPILE_WARNING_AS_ERROR=OFF"
        "-DENABLE_PLUGIN_LOADING=OFF" "-DWITH_REDUCED_VISIBILITY=ON" "-DWITH_EXAMPLES=OFF" "-DWITH_FUZZERS=OFF"
        "-DCMAKE_INSTALL_PREFIX:PATH=${EXTERNALPROJECT_BINARY_ROOT}/heif/install" "-DCMAKE_SHARED_LIBRARY_PREFIX=lib"
        "-DZLIB_ROOT:PATH=${ZLIB_ROOT}"
        "-DWITH_LIBDE265=ON" "-DLIBDE265_INCLUDE_DIR:PATH=${LIBDE265_INCLUDE_DIRS}" "-DLIBDE265_LINK_DIRS:PATH=${LIBDE265_LINK_DIRS}" "-DLIBDE265_LIBRARY:PATH=${LIBDE265_LIBRARY}"
        "-DWITH_KVAZAAR=ON" "-DKVAZAAR_INCLUDE_DIR:PATH=${KVAZAAR_INCLUDE_DIRS}" "-DKVAZAAR_LINK_DIRS:PATH=${KVAZAAR_LINK_DIRS}" "-DKVAZAAR_LIBRARY:PATH=${KVAZAAR_LIBRARY}"
        "-DWITH_DAV1D=ON" "-DDAV1D_INCLUDE_DIR:PATH=${DAVID_INCLUDE_DIRS}" "-DDAV1D_LINK_DIRS:PATH=${DAVID_LINK_DIRS}" "-DDAV1D_LIBRARY:PATH=${DAVID_LIBRARY}"
        "-DWITH_SvtEnc=ON" "-DSvtEnc_INCLUDE_DIR:PATH=${SVTAV1_INCLUDE_DIRS}" "-DSvtEnc_LINK_DIRS:PATH=${SVTAV1_LINK_DIRS}" "-DSvtEnc_LIBRARY:PATH=${SVTAV1_LIBRARY}"
        "-DWITH_AOM_DECODER=OFF" "-DWITH_AOM_ENCODER=OFF" "-DWITH_X265=OFF" "-DWITH_LIBSHARPYUV=OFF" "-DWITH_OpenH264_DECODER=OFF"
        "-DCMAKE_CXX_FLAGS=${CMAKE_CXX_FLAGS} ${DEF_FLAG}LIBDE265_STATIC_BUILD=1 ${DEF_FLAG}KVZ_STATIC_LIB=1"
        "-DCMAKE_C_FLAGS=${CMAKE_C_FLAGS} ${DEF_FLAG}LIBDE265_STATIC_BUILD=1 ${DEF_FLAG}KVZ_STATIC_LIB=1"
        "-DCMAKE_DEBUG_POSTFIX="
    EXCLUDE_FROM_ALL
    DEPENDS DE265 KVAZAAR DAVID SVTAV1
)

message(STATUS CMAKE_VS_MSBUILD_COMMAND=${CMAKE_VS_MSBUILD_COMMAND})

ExternalProject_Get_Property(HEIF SOURCE_DIR)
ExternalProject_Get_Property(HEIF BINARY_DIR)

add_library(LibHEIF INTERFACE)
add_dependencies(LibHEIF HEIF)

target_include_directories(LibHEIF INTERFACE ${SOURCE_DIR}/libheif/api ${BINARY_DIR})
set_property(TARGET HEIF PROPERTY FOLDER "Dependencies")

if (WIN32)
    set(LibHEIF_INSTALL_LIBRARY ${EXTERNALPROJECT_BINARY_ROOT}/heif/install/bin/heif.dll)
    set(LibHEIF_INSTALL_TYPE BIN)
    set(LibHEIF_INSTALL_NAME "heif.dll")
else()
    set(LibHEIF_INSTALL_LIBRARY ${EXTERNALPROJECT_BINARY_ROOT}/heif/install/lib/libheif.so.${HEIF_VERSION})
    set(LibHEIF_INSTALL_TYPE LIB)
    set(LibHEIF_INSTALL_NAME "libheif.so")
endif()

unset(SOURCE_DIR)
unset(BINARY_DIR)
